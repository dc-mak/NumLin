embed
  {{ tex-preamble
\renewcommand{\[[TEX_NAME_PREFIX]]interrule}{\\[2.0mm]}
  }}

metavar fc ::=
  {{ tex \mathit{[[fc]]} }} {{ com fractional capability variable }}


metavar x, g, a, b ::=
  {{ tex \mathit{[[x]]} }} {{ com expression variable }}


metavar k ::=
  {{ tex \mathit{[[k]]} }} {{ com integer variable }}


metavar el ::=
  {{ tex \mathit{[[el]]} }} {{ com array-element variable }}


metavar l ::=
  {{ tex \mathit{[[l]]} }} {{ com location variable }}


metavar M ::=
  {{ tex \mathit{[[M]]} }} {{ com matrix variable }}

grammar

  terminals {{ tex symb }} :: 'terminals_' ::=
    | \     ::  :: lambda    {{ tex \lambda       }}
    | ox    ::  :: product   {{ tex \otimes       }}
    | --o   ::  :: lollipop  {{ tex \multimap     }}
    | |-    ::  :: turnstile {{ tex \vdash        }}
    | mem   ::  :: member    {{ tex \in           }}
    | all   ::  :: all       {{ tex \forall       }}
    | Cap   ::  :: cap       {{ tex \textsf{Cap}  }}
    | Type  ::  :: type      {{ tex \textsf{Type} }}
    | !     ::  :: bang      {{ tex \: !          }}
    | ->    ::  :: arrow     {{ tex \rightarrow   }}
    | value ::  :: value
    | __    ::  :: hole      {{ tex [ - ]         }}
    | and   ::  :: disjU     {{ tex +             }}
    | |->   ::  :: points    {{ tex \mapsto       }}
    | <     ::  :: langle    {{ tex \langle       }}
    | >     ::  :: rangle    {{ tex \rangle       }}
    | <>    ::  :: neq       {{ tex \neq          }}
    | err   ::  :: err       {{ tex \textbf{err}  }}
    | @     ::  :: at        {{ tex \cdot         }}
    | ==    ::  :: judge_eq  {{ tex \equiv        }}


grammar

  m :: 'm_' ::=           {{ com matrix expressions     }}
    | M      ::  :: Var   {{ com matrix variables       }}
    | m + m' ::  :: Plus  {{ com matrix addition        }}
    | m m'   ::  :: Times {{ com matrix multiplication  }}
    | ( m )  :: S :: Paren


grammar

  f :: 'f_' ::=       {{ com fractional capability }}
    | fc  ::  :: Var  {{ com variable              }}
    | Z   ::  :: Zero {{ tex 1                     }}
                      {{ com whole capability      }}
    | S f ::  :: Succ {{ tex { \frac{1}{2} [[f]] } }}


  t :: 't_' ::=                    {{ com linear type               }}
    | unit         ::   :: Unit    {{ com unit                      }}
    | bool         ::   :: Bool    {{ com boolean (true/false)      }}
    | int          ::   :: Int     {{ com 63-bit integers           }}
    | elt          ::   :: Elt     {{ com array element             }}
    | f arr        ::   :: Array   {{ com arrays                    }}
    | f mat        ::   :: Matrix  {{ com matrices                  }}
    | ! t          ::   :: Bang    {{ com multiple-use type         }}
    | all fc . t   ::   :: Gen     (+ bind fc in t                  +)
                                   {{ com frac. cap. generalisation }}
    | t ox t'      ::   :: Pair    {{ com pair                      }}
    | t --o t'     ::   :: Lolly   {{ com linear function           }}
    | t [ f / fc ] :: M :: Sub     {{ com substitution              }}
    | ( t )        :: S :: Paren   {{ com parentheses               }}


grammar

   p :: 'p_' ::=                {{ com primitive                                   }}
     | not      ::  :: Not      {{ com boolean negation                            }}
     | ( + )    ::  :: Int_Add  {{ com integer addition                            }}
     | ( - )    ::  :: Int_Sub  {{ com integer subtraction                         }}
     | ( * )    ::  :: Int_Mul  {{ com integer multiplication                      }}
     | ( / )    ::  :: Int_Div  {{ com integer division                            }}
     | ( = )    ::  :: Int_Eq   {{ com integer equality                            }}
     | ( < )    ::  :: Int_Lt   {{ com integer less-than                           }}
     | ( +. )   ::  :: Elt_Add  {{ com element addition                            }}
     | ( -. )   ::  :: Elt_Sub  {{ com element subtraction                         }}
     | ( *. )   ::  :: Elt_Mul  {{ com element multiplication                      }}
     | ( /. )   ::  :: Elt_Div  {{ com element division                            }}
     | ( =. )   ::  :: Elt_Eq   {{ com element equality                            }}
     | ( <. )   ::  :: Elt_Lt   {{ com element less-than                           }}
     | set      ::  :: Set      {{ com array index assignment                      }}
     | get      ::  :: Get      {{ com array indexing                              }}
     | share    ::  :: Share    {{ com share array                                 }}
     | unshare  ::  :: Unshare  {{ com unshare array                               }}
     | free     ::  :: Free     {{ com free arrary                                 }}
     | array    ::  :: Array    {{ com Owl:  make array                            }}
     | copy     ::  :: Copy     {{ com Owl:  copy array                            }}
     | sin      ::  :: Sin      {{ com Owl:  map sine over array                   }}
     | hypot    ::  :: Hypot    {{ com Owl:  $x_i := \sqrt{x_i^2 + y_i^2}$         }}
     | asum     ::  :: Asum     {{ com BLAS: $\sum_i | x_i |$                      }}
     | axpy     ::  :: Axpy     {{ com BLAS: $x := \alpha x + y$                   }}
     | dot      ::  :: Dot      {{ com BLAS: $x \cdot y$                           }}
     | rotmg    ::  :: Rotmg    {{ com BLAS: see its docs                          }}
     | scal     ::  :: Scal     {{ com BLAS: $x := \alpha x$                       }}
     | amax     ::  :: Amax     {{ com BLAS: $\textrm{argmax}\,i:\,x_i$            }}
     | setM     ::  :: SetM     {{ com matrix index assignment                     }}
     | getM     ::  :: GetM     {{ com matrix indexing                             }}
     | shareM   ::  :: ShareM   {{ com share matrix                                }}
     | unshareM ::  :: UnshareM {{ com unshare matrix                              }}
     | freeM    ::  :: FreeM    {{ com free matrix                                 }}
     | matrix   ::  :: Matrix   {{ com Owl:  make matrix                           }}
     | copyM    ::  :: CopyM    {{ com Owl:  copy matrix                           }}
     | copyM_to ::  :: CopyM_to {{ com Owl:  copy matrix onto another              }}
                                {{ tex \textbf{copyM\_to}                          }}
     | sizeM    ::  :: SizeM    {{ com dimension of matrix                         }}
     | trnsp    ::  :: Trnsp    {{ com transpose matrix                            }}
     | gemm     ::  :: Gemm     {{ com BLAS: $C := \alpha A^{T?} B^{T?} + \beta C$ }}
     | symm     ::  :: Symm     {{ com BLAS: $C := \alpha A B + \beta C$           }}
     | posv     ::  :: Posv     {{ com BLAS: Cholesky decomp. and solve            }}
     | potrs    ::  :: Potrs    {{ com BLAS: solve with given Cholesky             }}
     | syrk     ::  :: syrk     {{ com BLAS: $C := \alpha A^{T?} A^{T?} + \beta C$ }}


grammar

  v :: 'v_' ::=                                       {{ com values                    }}
    | p                           ::   :: Prim        {{ com primitives                }}
    | x                           ::   :: Var         {{ com variable                  }}
    | ( )                         ::   :: Unit_Intro  {{ com unit introduction         }}
    | true                        ::   :: Bool_True   {{ com true                      }}
    | false                       ::   :: Bool_False  {{ com false                     }}
    | k                           ::   :: Int_Intro   {{ com integer                   }}
    | l @ f                       ::   :: Location    {{ com heap location             }}
    | el                          ::   :: Elt_Intro   {{ com array element             }}
    | Many v                      ::   :: Bang_Intro  {{ com !-introduction            }}
    | fun fc -> v                 ::   :: Gen         {{ com frac. cap. abstraction    }}
    | v [ f ]                     ::   :: Spc         {{ com frac. cap. specialisation }}
    | ( v , v' )                  ::   :: Pair_Intro  {{ com pair introduction         }}
    | fun x : t -> e              ::   :: Lambda      (+ bind x in e                   +)
                                                      {{ com abstraction               }}
    | fix ( g , x : t , e : t' )  ::   :: Fix         (+ bind g union x in e           +)
                                                      {{ com fixpoint                  }}
    | v [ fc / f ]                :: M :: Sub_fc      {{ com substitution              }}
    | ( v )                       :: S :: Paren       {{ com parentheses               }}


  e :: 'e_' ::=                                       {{ com expression                }}
    | p                           ::   :: Prim        {{ com primitives                }}
    | x                           ::   :: Var         {{ com variable                  }}
    | let x = e in e'             ::   :: Let         (+ bind x in e'                  +)
                                                      {{ com let binding               }}
    | ( )                         ::   :: Unit_Intro  {{ com unit introduction         }}
    | let ( ) = e in e'           ::   :: Unit_Elim   {{ com unit elimination          }}
    | true                        ::   :: Bool_True   {{ com true                      }}
    | false                       ::   :: Bool_False  {{ com false                     }}
    | if e then e1 else e2        ::   :: Bool_Elim   {{ com if                        }}
    | k                           ::   :: Int_Intro   {{ com integer                   }}
    | l @ f                       ::   :: Location    {{ com heap location             }}
    | el                          ::   :: Elt_Intro   {{ com array element             }}
    | Many e                      ::   :: Bang_Intro  {{ com !-introduction            }}
    | let Many x = e in e'        ::   :: Bang_Elim   {{ com !-elimination             }}
    | fun fc -> e                 ::   :: Gen         {{ com frac. cap. abstraction    }}
    | e [ f ]                     ::   :: Spc         {{ com frac. cap. specialisation }}
    | ( e , e' )                  ::   :: Pair_Intro  {{ com pair introduction         }}
    | let ( a , b ) = e in e'     ::   :: Pair_Elim   (+ bind a union b in e'          +)
                                                      {{ com pair elimination          }}
    | fun x : t -> e              ::   :: Lambda      (+ bind x in e                   +)
                                                      {{ com abstraction               }}
    | e e'                        ::   :: App         {{ com application               }}
    | fix ( g , x : t , e : t' )  ::   :: Fix         (+ bind g union x in e           +)
                                                      {{ com fixpoint                  }}
    | e [ fc / f  ]               :: M :: Sub_fc      {{ com substitution              }}
    | e [ x / e'  ]               :: M :: Sub         {{ com substitution              }}
    | ( e )                       :: S :: Paren       {{ com parentheses               }}
    | C [ e ]                     :: M :: Context     {{ com evaluation context        }}

  C :: 'C_' ::=                                      {{ com evaluation contexts       }}
    | let x = __ in e             ::   :: Let        (+ bind x in e                   +)
                                                     {{ com let binding               }}
    | let ( ) = __ in e           ::   :: Unit_Elim  {{ com unit elimination          }}
    | if __ then e1 else e2       ::   :: Bool_Elim  {{ com if                        }}
    | Many __                     ::   :: Bang_Intro {{ com !-introduction            }}
    | let Many x = __ in e        ::   :: Bang_Elim  {{ com !-elimination             }}
    | fun fc -> __                ::   :: Gen        {{ com frac. cap. abstraction    }}
    | __ [ f ]                    ::   :: Spc        {{ com frac. cap. specialisation }}
    | ( __ , e )                  ::   :: Pair_Fst   {{ com pair introduction         }}
    | ( v , __ )                  ::   :: Pair_Snd   {{ com pair introduction         }}
    | let ( a , b ) = __ in e     ::   :: Pair_Elim  (+ bind a union b in e           +)
                                                     {{ com pair elimination          }}
    | __ e                        ::   :: App_Fun    {{ com application               }}
    | v __                        ::   :: App_Arg    {{ com application               }}
%   | ( __ )                      :: S :: Paren      {{ com parentheses               }}


subrules
  v <:: e


contextrules
  C _:: e :: e


grammar

  T {{ tex \Theta }} :: 'T_' ::= {{ com fractional capability environment }}
    | nil    ::   :: Nil {{ tex \cdot }}
    | T , fc ::   :: Ext


  G {{ tex \Gamma }} :: 'G_' ::= {{ com linear types environment }}
    | nil       ::   :: Nil {{ tex \cdot }}
    | G , x : t ::   :: Ext
    | G , G'    ::   :: Split


  D {{ tex \Delta }} :: 'D_' ::= {{ com intuitionistic types environment }}
    | nil       ::   :: Nil {{ tex \cdot }}
    | D , x : t ::   :: Ext


defns
  Well_Formed :: 'WF_' ::=


  defn
  T |- f Cap :: ::Cap_:: 'Cap_' {{ com Valid fractional capabilities }} by


    fc mem T
    ----------- :: Var
    T |- fc Cap


    ---------- :: Zero
    T |- Z Cap


    T |- f Cap
    ------------ :: Succ
    T |- S f Cap


  defn
  T |- t Type :: ::Type_:: 'Type_' {{ com Valid types }} by


    -------------- :: Unit
    T |- unit Type


    -------------- :: Bool
    T |- bool Type


    ------------- :: Int
    T |- int Type


    ------------- :: Elt
    T |- elt Type


    T |- f Cap
    --------------- :: Array
    T |- f arr Type


    T |- t Type
    ------------- :: Bang
    T |- ! t Type


    T, fc |- t Type
    ------------------------ :: Gen
    T     |- all fc . t Type


    T |- t Type
    T |- t' Type
    ----------------- :: Pair
    T |- t ox t' Type


    T |- t Type
    T |- t' Type
    ---------------- :: Lolly
    T |- t --o t' Type


defns
  Types :: 'Ty_' ::=


  defn
  T ; D ; G |- e : t :: ::Type_:: '' {{ com Typing rules for expressions }} by


    ---------------------- :: Var_Lin
    T ; D ; nil, x : t |- x : t


    x : t mem D
    ---------------------- :: Var
    T ; D ; nil |- x : t


    T ; D ; G          |- e  : t
    T ; D ; G' , x : t |- e' : t'
    -------------------------------------- :: Let
    T ; D ; G , G' |- let x = e in e' : t'


    --------------------------- :: Unit_Intro
    T ; D ; nil |- ( ) : unit


    T ; D ; G  |- e  : unit
    T ; D ; G' |- e' : t
    -------------------------------------- :: Unit_Elim
    T ; D ; G, G' |- let ( ) = e in e' : t


    ---------------------------- :: Bool_True
    T ; D ; nil |- true : bool


    ----------------------------- :: Bool_False
    T ; D ; nil |- false : bool


    T ; D ; G  |- e  : ! bool
    T ; D ; G' |- e1 : t'
    T ; D ; G' |- e2 : t'
    ------------------------------------------ :: Bool_Elim
    T ; D ; G , G' |- if e then e1 else e2 : t


    ------------------------ :: Int_Intro
    T ; D ; nil |- k : int


    ------------------------- :: Elt_Intro
    T ; D ; nil |- el : elt


    T ; D ; nil |- v : t
    v <> l @ f
    --------------------------- :: Bang_Intro
    T ; D ; nil |- Many v : ! t


    T ; D ; G          |- e : ! t
    T ; D , x : t ; G' |- e' : t'
    ------------------------------------------ :: Bang_Elim
    T ; D ; G, G' |- let Many x = e in e' : t'


    T ; D ; G  |- e  : t
    T ; D ; G' |- e' : t'
    -------------------------------------- :: Pair_Intro
    T ; D ; G , G' |- ( e , e' ) : t ox t'


    T ; D ; G                  |- e12 : t1 ox t2
    T ; D ; G', a : t1, b : t2 |- e : t
    ---------------------------------------------- :: Pair_Elim
    T ; D ; G, G' |- let ( a , b ) = e12 in  e : t


    T                 |- t' Type
    T ; D ; G, x : t' |- e : t
    --------------------------------------- :: Lambda
    T ; D ; G |- fun x : t' -> e : t' --o t


    T ; D ; G  |- e  : t' --o t
    T ; D ; G' |- e' : t'
    --------------------------- :: App
    T ; D ; G, G' |- e e' : t


    T , fc ; D ; G |- e : t
    ------------------------------------- :: Gen
    T ; D ; G |- fun fc -> e : all fc . t


    T |- f Cap
    T ; D ; G |- e : all fc . t
    ----------------------------------- :: Spc
    T ; D ; G |- e [ f ] : t [ f / fc ]


    T ; D , g : t --o t'  ; nil, x : t |- e : t'
    ---------------------------------------------------------- :: Fix
    T ; D ; nil |- fix ( g , x : t , e : t' ) : t --o t' 


grammar

  H {{ tex \sigma }} :: 'Heap_' ::=  {{ com heap (multiset of triples) }}
    | nil :: :: Nil  {{ tex \{ \} }} {{ com empty heap }}
    | H and { l |-> f , m k1 k2 } :: :: Ext 
    {{ tex [[H]] [[and]] \{ [[l]] [[|->]]_[[f]] [[m]]_{[[k1]], [[k2]]} \} }}
    {{ com location [[l]] points to matrix [[m]] }}


  StepsTo :: 'StepsTo_' ::= {{ com result of small step }}
    | < H , e > :: :: Ok    {{ com heap and expression  }}
    | err       :: :: Error {{ com error                }}


defns

  Op_Sem :: 'Op_' ::=

  defn
  < H , e > -> StepsTo :: ::Op_:: '' {{ com operational semantics }} by


    --------------------------------------- :: Let_Unit
    < H , let ( ) = ( ) in e > -> < H , e >


    --------------------------------------------- :: Let_Var
    < H , let x = v in e > -> < H , e [ x / v ] >


    --------------------------------------------- :: If_True
    < H , if ( Many true ) then e1 else e2 > -> < H , e1 >


    ---------------------------------------------- :: If_False
    < H , if ( Many false) then e1 else e2 > -> < H , e2 >


    ------------------------------------------------------- :: Let_Many
    < H , let Many x = Many v in e > -> < H , e [ x / v ] >


    --------------------------------------------------------------------------- :: Let_Pair
    < H , let ( a , b ) = ( v1 , v2 ) in e > -> < H , e [ a / v1 ] [ b / v2 ] >


    ---------------------------------------------------- :: Frac_Cap
    < H , ( fun fc -> v ) [ f ] > -> < H , v [ fc / f ] >


    ---------------------------------------------------------------------------------------------- :: App_Fix
    < H , fix ( g , x : t , e : t' ) v > -> < H , e [ x / v ] [ g / fix ( g , x : t , e : t' ) ] >


    --------------------------------------------------- :: App_Lambda
    < H , ( fun x : t -> e ) v > -> < H , e [ x / v ] >


    < H , e >  -> < H' , e' >
    ----------------------------------- :: Context
    < H , C [ e ] > -> < H , C [ e' ] >


    < H , e >  -> err
    ---------------------- :: Context_Err
    < H , C [ e ] > -> err


    0 <= k1 , k2
    l fresh
    ---------------------------------------------------------------- :: Matrix
    < H , matrix k1 k2 > -> < H and { l |-> Z ,  M k1 k2 } , l @ Z >


    ----------------------------------------------------------- :: Free
    < H and { l |-> Z , m k1 k2 } , free l @ Z > -> < H , ( ) >


    --------------------------------------------------------------------------------------------------------------------------------------- :: Share
    < H and { l |-> f , m k1 k2 } , share l @ f  > -> < H and { l |-> S f , m k1 k2 } and { l |-> S f , m k1 k2 } , ( l @ S f , l @ S f ) >


    f <= 1
    v == l @ S f
    ---------------------------------------------------------------------------------------------------------------------- :: Unshare_Eq
    < H and { l |-> S f , m k1 k2 } and { l |-> S f , m k1 k2 } , unshare v v > -> < H and { l |-> f , m k1 k2 } , l @ f >


    l <> l'
    ---------------------------------------------------------------------------------------------------------- :: Unshare_Neq
    < H and { l |-> S f , m k1 k2 } and { l' |-> S f , m' k1 k2 } , unshare ( l @ S f ) ( l' @ S f' ) > -> err


    H' == H and { l1 |-> fc1 , m1 k1 k2 } and { l2 |-> fc2 , m2 k2 k3 }
    v1 == l1 @ f1
    v2 == l2 @ f2
    v3 == l3 @ Z
    --------------------------------------------------------------------------------------------------------------------------- :: Gemm_Match
    < H' and { l3 |-> Z , m3 k1 k3 } , gemm v1 v2 v3 > -> < H' and { l3 |-> Z , ( m1 m2 + m3 ) k1 k3 } , ( ( v1 , v2 ) , v3 ) >


    k2 <> k2'
    H' == H and { l1 |-> fc1 , m1 k1 k2 } and { l2 |-> fc2 , m2 k2' k3 }
    v1 == l1 @ f1
    v2 == l2 @ f2
    v3 == l3 @ Z
    --------------------------------------------------------- :: Gemm_Mismatch
    < H' and { l3 |-> Z , m1 k1 k3 } , gemm v1 v2 v3 > -> err


grammar

  formula :: 'formula_' ::=
    | judgement    ::  :: judgement
    | x : t mem D  ::  :: xtD
    | x : t mem G  ::  :: xtG
    | fc    mem T  ::  :: fcT
    | e <> e'      ::  :: eNeq
    | l <> l'      ::  :: lNeq
    | e == e'      ::  :: eEq
    | 0 <= k1 , k2 ::  :: posDim {{ tex [[0]] \leq [[k1]], [[k2]]  }}
    | f <= 1       ::  :: fracBound {{ tex [[f]] \leq 1  }}
    | H' == H      ::  :: storeEq
    | l fresh      ::  :: freshL {{ tex [[l]] \textrm{ fresh } }}


% vim: sw=2 sts=2
